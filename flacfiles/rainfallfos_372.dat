; new
config tpflow extra 5

def setup
    IO_READ = 0
    IO_WRITE = 1
    IO_FISH = 0
    IO_ASCII = 1
    tempFilename = 'temp.txt'
    rstFilename = 'result2.txt'
end
setup


def io_c_phi
    int ipt_num 
    float ipt_c1 ipt_c2 ipt_c3 ipt_f1 ipt_f2 ipt_f3 ipt_num
    array iptRaw(2)
    status1=open(tempFilename,IO_READ,IO_ASCII)

    status2=read(iptRaw,1)

    status3=close

    cal_num=int(parse(iptRaw(1),1))
    ; c and phi
    c_eff=float(parse(iptRaw(1),2))
    phi_eff=float(parse(iptRaw(1),3))
    ; swcc para a and p0
    van_a=float(parse(iptRaw(1),4))
    van_p0=float(parse(iptRaw(1),5))
    ; ksat 7.08e-11
    ksat=float(parse(iptRaw(1),6))

    ;rainfall amount
    rf_amt=float(parse(iptRaw(1),7))
    ; rf_amt=2e-7

    ; rainfall time 
    rf_time = float(parse(iptRaw(1),8))
    ; rf_time = 2160000

    ; ini suction
    inisuction=-float(parse(iptRaw(1),9))
    ; inisuction=-100000
    van_n = 1.0/(1.0-van_a)
    se_ini = ((-inisuction/van_p0)^van_n + 1.0)^(-van_a)
    theta_s = 0.46
    theta_r = 0.2
    theta_ini = se_ini*(theta_s-theta_r)+theta_r
        ; sw_ini = se_ini * (1.0 - rs_1) + rs_1
    gs = 2.6
end
io_c_phi

grid 90,40
gen 0.0,0.0 0.0,5.0 10.0,5.0 10.0,0.0 i=1,21 j=1,11
gen 10.0,0.0 10.0,5.0 45.0,5.0 45.0,0.0 i=21,91 j=1,11
gen 10.0,5.0 29.55,20.0 45.0,20.0 45.0,5.0 i=21,91 j=11,41
model elastic i=1,90 j=1,10
model elastic i=21,90 j=11,40

; model mohr
prop dens 1950 s = 5.38462E6 b = 1.16667E7

initial poro theta_s
initial wpermeability ksat
initial vga van_a
initial vgpcw 0.5
initial vgpcnw 0.5
initial rsat theta_r
initial vgp0 van_p0
initial saturation theta_ini
initial pp inisuction
initial fmodulus 1e5
initial f2modulus 1
;变形引起的孔压变化
initial udcoe 0
water density 1000.0 nwdensity 0.0
fix  x y i 1
fix  x y i 91
fix  x y j 1
set gravity=10.0
set step 500000
def setDischarge
    dischargeAmount = rf_amt
    dischargeAmountS = dischargeAmount/1.414
end

setDischarge

apply discharge dischargeAmount from 22,41 to 91,41
apply discharge dischargeAmountS from 21,11 to 21,41
apply discharge dischargeAmount from 1,11 to 20,11

initial nwpp 0.0 i 21,91 j 41 
initial nwpp 0.0 i 21 j 12,40
initial nwpp 0.0 i 1,21 j 11

fix nwpp
set mechanical off flow on
solve age rf_time

set flow=off
set mechanical=on

def get_ex3
    loop i (1,izones)
        if i<21
            loop j(1,10)
                ex_3(i,j)=-((gpp(i,j)+gpp(i+1,j)+gpp(i,j+1)+gpp(i+1,j+1))/4)
            end_loop
        else
            loop j (1,jzones)
                ex_3(i,j)=-((gpp(i,j)+gpp(i+1,j)+gpp(i,j+1)+gpp(i+1,j+1))/4)
            end_loop
        end_if
    end_loop
end
get_ex3



;---- reset displacement to zero----
ini xv 0 yv 0 xd 0 yd 0
set step 10000


model m i=1,90 j=1,10
model m i=21,90 j=11,40
def update_den_coh
    

    ; pd=1950.0

    cohsat=c_eff
    phi=phi_eff
 

    loop i (1,90)
        loop j (1,40)

            region_flag  = 0
        
            if i<21 then
                if j>10 then
                  region_flag = 1
                end_if
            end_if

            if region_flag = 0

                if ex_3(i,j)<=0 then
                    command
                        prop coh cohsat i=i j=j
                    end_command
                    ex_2(i,j) = cohsat
                else

                    
                    ; Se = ((S-Sr)/(1-Sr)) 
                    Se = ((ex_3(i,j)/van_p0)^van_n + 1.0)^(-van_a)
                    totalcoh=cohsat+ex_3(i,j)*tan(phi*degrad)*(Se)
                    ex_2(i,j) = totalcoh

                    theta_curr = Se*(theta_s-theta_r)+theta_r
                    ; todo
                    ; pt=pd*(1+w) ; increase density due to water infiltration
                    pt = (gs*(1-theta_s)+theta_curr)*1000
                    ; if ex_3(i,j)<=ex_1(i,j)
                    command
                        prop den pt i=i j=j
                    end_command
                    ; end_if
                    
                    command
                        prop coh totalcoh i=i j=j
                    end_command

                end_if

            end_if

        end_loop
    end_loop


end

update_den_coh

mark i = 1,21 j = 11
mark i = 21 j = 11,41
model null region 2,20
prop s = 5.38E6 b = 1.16E7 fri = phi_eff ten = 0 region 51,20
fix x y i = 1,2
fix x y i = 90,91
fix x y j = 1,2
set grav = 9.8
; save
; solve elastic
; ini xv 0 yv 0 xd 0 yd 0
; solve step 10000
solve fos associated file result2.fsv no_r

def getfos
    ; fosoutput = mech_ratio
    fosoutput = fos
    o_size = 1
end
getfos

def io_fos
    array output(o_size)
    temp1 = string(cal_num)+' '+string(c_eff)+' '
    temp2 = string(phi_eff)+' '+string(van_a)+' '
    temp3 = string(van_p0)+' '+string(ksat)+' '
    temp4 = string(rf_amt)+' '+string(rf_time)+' '
    temp5 = string(inisuction)+' '
    rst = string(fosoutput)
    output(1) = temp1+temp2+temp3+rst
    status = open(rstFilename, IO_WRITE, IO_ASCII)
    status = write(output,o_size)
    status = close
end
io_fos

quit